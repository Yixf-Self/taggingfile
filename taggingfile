#!/usr/bin/env perl
use strict;
use Getopt::Long;
use File::Path qw(make_path remove_tree);
use File::Find;
use File::Basename;

my $usage = <<USAGE;

taggingfile V1.1

Function : Cataloging files by tags (Only for *nix right now!!!)
Author   : Wei Shen <shenwei356\@gmail.com> <http://shenwei.me>
Website  : https://github.com/shenwei356/taggingfile
Created  : 2014-07-03
Update   : 2014-08-14

Usage: taggingfile [options] [directory ...]

Options:

    -h                   Print usage.
    -help                Print usage and details.
    -s --suffix PATTERN  File suffix for filter of the files, 
                         cases ignored. [.*]
    -p --prefix STRING   Tag directory prefix ["_TAG_"]
    -f --file            Handle files.
    -d --dir             Handle directories.
    -n --depth  INT      Maximum search depth.
    -clear               Remove all the tag directories.
    -renew               Remove and recreate all the tags directories.

-f or/and -d should be specified.

Use "taggingfile --help" for usage and details.

Examples:

1. Recursively tagging all the files in current directories, 

    taggingfile -f 

2. When big changes happen, for example, file or tag deleted.

    taggingfile -f -renew

3. Recursively tagging pdf files in "pdfs" directory

    taggingfile -f -s .pdf pdf

4. Only tagging directories of **first level** in directory "soft" and "soft2" .

    taggingfile -d -n 1 soft soft2

USAGE

my $detail = <<DETAIL;

Motivation:

I simply cataloged articles PDF files by storing them in different 
directories which stands for different categories. However some
articles belong to several fields. It made me crazy for cataloging
them.

It's not a good practice to copy them to different directories, 
because it will occupy more disk space and the modify only act for
one copy of the file.

Symbolic link is a good solution for low disk usage and file consistency.

How:

- File organization:

    - The hierarchic directories in current path countain the targets files.

    - Every file in the directory would be paired with a tag file,
        for example, "a.txt" is paired with "a.txt.tag". 
- Tag:

    - Tag file countains tags in multline. If no tag file given
        or no tags found in tag file. The file will be categorized 
        as "uncategorized".

    - Hierarchic tag is supported, e.g. "Tag, Important", directory tree 
        "Tag/Important" will be created.

- This script should be excuted in same path with the directory.

- File suffix (optional) is for filter of the files.

- Directories with name of tags will be created in current path.

- The symlink uses relative path. Unlinked symlinks will NOT be 
   removed every excution for security unless -renew option is specified.

DETAIL

my ( $h, $help, $suffix, $prefix, $renew, $clear )
    = ( undef, undef, ".*", "_TAG_", undef, undef );
my ( $handle_file, $handle_dir, $depth ) = ( 0, 0, undef );

# for saving the tag directory prefixes for renew
my $tag_prefix_history_file = ".tag_prefix_history";
my $prefixes                = {};

GetOptions(
    'h'          => \$h,
    'help'       => \$help,
    'suffix|s=s' => \$suffix,
    'prefix|p=s' => \$prefix,
    'file|f'     => \$handle_file,
    'dir|d'      => \$handle_dir,
    'depth|n=i'  => \$depth,
    'renew'      => \$renew,
    'clear'      => \$clear
    )
    or die qq(
Use "taggingfile -h"    for usage
Use "taggingfile -help" for usage and details

);

die $usage if $h;
die "$usage\n$detail" if $help;

die "Prefix length should not be greater than 3. Your input: $prefix\n"
    unless length $prefix >= 3;

unless ( $clear or $renew ) {
    die "-f or/and -d should be specified.\n"
        unless $handle_dir or $handle_file;
}

my @dirs = @ARGV;

@dirs = glob "*" if @dirs == 0;

$prefix .= "_";
@dirs = grep { -d and not /^\.+/ and not /^$prefix/ } @dirs;

if ( -e $tag_prefix_history_file ) {
    $prefixes = &get_tag_prefix_history($tag_prefix_history_file);
}
else {
    $$prefixes{$prefix}++;
}

# delete and recreate directories for all tags.
if ($renew) {
    &clear_tag_directories($prefixes);
}
elsif ($clear) {
    &clear_tag_directories($prefixes);
    exit 0;
}

# get file list
my $files = [];
for my $dir (@dirs) {
    push @$files, @{
        &get_file_list(
            $dir,
            sub {
                my ($file) = @_;

                # tag file and configure file
                if ( $file =~ /\.tag/i or $file =~ /^\./i ) {
                    return 0;
                }

                # check suffix
                if ( $file !~ /$suffix$/i ) {
                    return 0;
                }

                if ( -d $file ) {
                    if ($handle_dir) {
                        return 1;
                    }
                }
                else {
                    if ($handle_file) {
                        return 1;
                    }
                }

                return 0;
            },
            $depth
        )
    };
}

# create symlink
my $n = 0;
for my $file (@$files) {
    my $tags = &get_tags($file);
    &create_symlink( $file, $tags );
    $n++;
}
printf "$n files/directories been cataloged.\n";

# save the tag directory prefixes for renew
$$prefixes{$prefix}++;
&save_tag_prefix_history( $tag_prefix_history_file, $prefixes );

sub clear_tag_directories(/%) {
    my ($prefixes) = @_;
    my @dirs = grep { -d and not /^\./ } glob "*";
    my @tag_directories = ();
    for my $dir (@dirs) {
        for my $prefix ( keys %$prefixes ) {
            if ( $dir =~ /^$prefix/ ) {
                push @tag_directories, $dir;
            }
        }
    }

    # remove old tag directories
    for my $dir (@tag_directories) {
        remove_tree($dir) or die "fail to remove directory: $dir. $!\n";
    }

    # reset
    $prefixes = {};
    &save_tag_prefix_history( $tag_prefix_history_file, $prefixes );
}

sub get_tag_prefix_history($) {
    my ($file) = @_;
    my $prefixes = {};
    open FH, "<", $file
        or die "fail to open file: $file. $!\n";
    while (<FH>) {
        s/\r?\n//g;
        s/^\s+//;
        s/\s+$//;
        next if $_ eq "";
        $$prefixes{$_}++;
    }
    close FH;
    return $prefixes;
}

sub save_tag_prefix_history($/%) {
    my ( $file, $prefixes ) = @_;
    open FH, ">", $file
        or die "fail to write file: $file. $!\n";
    for my $prefix ( sort keys %$prefixes ) {
        print FH "$prefix\n";
    }
    close FH;
}

sub create_symlink($/@) {
    my ( $file, $tags ) = @_;
    for my $tag (@$tags) {
        $tag = "$prefix$tag";

        if ( not -e $tag ) {
            make_path($tag) or die "fail to mkdir: $tag. $!\n";
        }

        # symlink for target file
        my $link_file = "$tag/" . basename($file);
        if ( -e "$link_file" ) {
            unlink "$link_file" or die "fail to unlike: $link_file. $!\n";
        }
        my $ok = symlink "../" x ( 1 + $tag =~ tr/\/// ) . "$file",
            $link_file;
        if ( not $ok ) {
            warn "fail to create symlink for $file. $!\n";
            next;
        }

        # symlink for tag file
        my $tag_file = "$tag/" . basename($file) . ".tag";
        if ( -e "$tag_file" ) {
            unlink "$tag_file" or die "fail to unlike: $tag_file. $!\n";
        }
        my $ok = symlink "../" x ( 1 + $tag =~ tr/\/// ) . "$file.tag",
            $tag_file;
        if ( not $ok ) {
            warn "fail to create symlink for $file.tag. $!\n";
            next;
        }
    }
}

sub get_tags($) {
    my ($file)      = @_;
    my $tag_file    = "$file.tag";
    my $tags        = [];
    my $default_tag = "uncategorized";

    if ( not -e $tag_file ) {    # no tag file
        open FH, ">", $tag_file
            or die "fail to write file $tag_file. $!\n";
        print FH "$default_tag\n";
        close FH;
        return [$default_tag];
    }

    open FH, "<", $tag_file
        or die "fail to open file $tag_file. $!\n";
    while (<FH>) {
        s/\r?\n//g;
        s/^\s+//;
        s/\s+$//;
        s/[\/\\]+//g;
        next if $_ eq "";

        $_ = join "/", split( /,\s*/, $_ );

        push @$tags, lc $_;    # change to lowercase
    }
    close FH;

    if ( @$tags == 0 ) {       # no tags
        return [$default_tag];
    }
    return $tags;
}

sub get_file_list {

    # filter is a subroutine to filter a file
    my ( $dir, $filter, $depth ) = @_;
    $dir =~ s/\/+/\//g;
    $dir =~ s/\/$//;

    $depth = 1 << 30 unless defined $depth;
    unless ( $depth =~ /^\d+$/ and $depth > 0 ) {
        warn "depth should be positive integer\n";
        return [];
    }
    # print "$dir\n";
    my $depth0 = $dir =~ tr/\//\//;

    my $files  = [];
    my $wanted = sub {
        return if /^\.+$/;
        return if $_ eq $dir;

        # check depth
        return if $File::Find::name =~ tr/\//\// - $depth0 > $depth;

        if ( &$filter($_) ) {
            push @$files, $File::Find::name;
        }
    };

    find( $wanted, ($dir) );

    return $files;
}
